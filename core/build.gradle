apply plugin: 'maven-publish'

dependencies {
    def v = "2.3.7.RELEASE"
    try {
        v = java.util.Optional.ofNullable(springBootVersion).orElse(v)
    } catch (MissingPropertyException e) {}
    compileOnly "org.springframework.boot:spring-boot-starter-web:${v}"
}

def coreVersion = '0.1-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId "http-raw-log"
            version coreVersion
            from components.java

            pom {
                name = 'skycong http raw log kit'
                description = 'print http raw log'
                url = 'https://gitee.com/randomsky/http-raw-log'
                properties = [
                        springBootVersion : "2.3.7.RELEASE"
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rmc'
                        name = 'rmc'
                        email = 'mc@skycong.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://gitee.com/randomsky/http-raw-log'
                    developerConnection = 'scm:git:https://gitee.com/randomsky/http-raw-log.git'
                    url = 'https://gitee.com/randomsky/http-raw-log'
                }
            }
        }
    }

    repositories {
        maven {
            url project.version.endsWith("-SNAPSHOT") ? nexusSnapshotUrl : nexusReleaseUrl
            credentials {
                username username1
                password password1
            }
        }
    }
}